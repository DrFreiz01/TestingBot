theme: /helloBye


    state: myNameIs
        q!: [$beginningWords] я [$interjections] $namesRus
        q!: [$beginningWords] ты зови (менч/меня/мня) $namesRus
        q!: [$beginningWords] $meGenAcc (зовут/зоут/ховут/звать/звут) $namesRus
        q!: [$beginningWords] $namesRus $meGenAcc (зовут/зоут/ховут/звать/звут)
        q!: [$beginningWords] $meGenAcc $namesRus (зовут/зоут/ховут/звать/звут)
        # 4EX: Здравствуйте! Пишет Александр из Санкт-Петербурга!
        q!: [$beginningWords] [$hello2/$hi] [к] [$youDat] (пишет/обращается/обращаеться) $namesRus [$from] [$iataAndRailway]
        script:
            $client.name = $parseTree._namesRus.name;
        a: Приятно познакомиться, {{$client.name}}!


    state: hello
        # 4EX: привет
        # 4EX: всем привет
        q!: [$beginningWords] [всем] [$morph<П>]($hello2/$hi) [$youDat][ [$morph<П>] $botVocAll]
        q!: [$beginningWords] [всем] [$morph<П>]($hello2/$hi) ($hello2/$hi) [$youDat][ [$morph<П>] $botVocAll]
        q!: добры*
        # 4EX: ещё раз здравствуйте
        q!: еще раз ($hello2/$hi)
        # 4EX: Здраствуйте! Кто нибудь есть?
        q!: [$beginningWords] [всем] [$morph<П>]($hello2/$hi) {($who/$anybody) (есть/есь)}[$markQuestion]

        if: $context.currentState == $client.executeState
            a: Приветствую еще раз!
        else:
            script:
                $client.executeState = $context.currentState;
            a: Здравствуйте! Чем я могу вам помочь?


        state: react_HelloHow
            q: * {как* * (дела/жизн*/делиш*/живешь/живете/поживае*)} *
            q: * как* [$youBotAll] сам*
            q: * как* $youBotAll
            a: Большое спасибо! У меня все отлично!


            state: react_WhatAreYouUpTo
                q: * $what2 [$youBotAll] (делаешь/делаишь/делаеш/делаиш/поделываеш*/поделываиш*/делает/делаит/поделывает/поделываит) *
                q: * чем * (занимаеш*/занимаиш*/занят*) *
                a: Отвечаю на ваши вопросы


    state: howAreYou
        # 4EX: как дела
        q!: [$beginningWords] {как  ([$your2/у $youGenAcc](дела/жизн*/делиш*)/сам/сама/живешь/пожив*) } [$markQuestion]
        # 4EX: как ты?
        q!: [$beginningWords] (как ты/какты) [$markQuestion]
        # 4EX: какие дела
        q!: какие дела [$markQuestion]
        random:
            a: Спасибо, все прекрасно!
            a: Спасибо, все замечательно! А у вас?
            a: У меня все отлично! Надеюсь, у вас тоже все хорошо.


        state: react_IAmNotFine
            q: * $ruUMenya (нет/нпт/неееееееееет/нееееееееет/неееееееет/нееееееет/неееееет/нееееет/неееет/нееет/неет) *
            q: * (плохо/$bad) *
            a: Возможно, я смогу помочь. Задайте ваш вопрос.


        state: react_IAmFine
            q: * ($good2/норм*/нарм*) *
            q: [$thanks2] {[все] ($good2/норм*/нарм*) [$ruUMenya]} [$thanks2]
            a: Отлично!


    state: isThereAnybodyHere
        # 4EX: ау есть тут кто?
        q!: [$beginningWords] (есть/есь) {($here/там/тама) ($who/$anybody) $more}[$markQuestion]
        # 4EX: ну тут есть кто нибудь еще?
        q!: [$beginningWords] ($here/там/тама) {(есть/есь) ($who/$anybody) $more}[$markQuestion]
        # 4EX: а кто нибудь тут есть?
        q!: [$beginningWords] ($who/$anybody) {($here/там/тама) (есть/есь) $more}[$markQuestion]
        # 4EX: еще кто нибудь тут есть
        q!: [$beginningWords] $more {($who/$anybody) ($here/там/тама) (есть/есь)}[$markQuestion]
        # 4EX: Есть здесь кто?
        q!: [$beginningWords] {(есть/есь) ($here/там/тама) ($who/$anybody)}[$markQuestion]
        # 4EX: есть кто??
        q!: [$beginningWords] {(есть/есь) ($who/$anybody)}[$markQuestion]
        a: Да, конечно! Готов ответить на ваши вопросы.


    state: whatsUp
        # 4EX: че как
        q!: [$beginningWords] (чо/че/чё) (как/какк/ккак/каг)
        # 4EX: как ты тама
        q!: [$beginningWords] (как/какк/ккак/каг) $you [тама/там/$ruVoobshe ]
        # 4EX: как оно там
        q!: [$beginningWords] (как/какк/ккак/каг) (оно/ано/сам/сама) [тама/там/$ruVoobshe]
        a: У меня все отлично! Надеюсь, у вас тоже все хорошо.


        state: react_WhatsUpGood
            q: * $yes2 *
            q: * а как же [иначе] *
            q: * $certainly [же] *
            q: [$beginningWords/все] (гуд/ато/атож/есть такое/а то ж/а тож)
            q: [$beginningWords/все] (очень/очинь/очен/ощень/ощен/весьма/висьма/весма/висма/вполне/нереальн*/ниреальн*/нериальн*/нириальн*/крайне/чрезвычайно/черезвычайно)
            q: [$beginningWords] $yes2 $veryAll
            q: [$beginningWords] (вроде/типа/тип) того
            q: [$beginningWords/все] $good2
            q: * $good2 *
            q: * {($ruUMenya) * $good2} *
            q: * (тоже/то же) *
            q: * и $ruUMenya
            random:
                a: Прекрасно!
                a: Рад слышать!



    state: mood
        # 4EX: как настроение
        q!: * {как [у тебя/твое] * (настроение/настроень*/ностроение/ностроень*/ностраение/ностраень*/настраение/настраень*/настроенние)} *
        a: У меня отличное настроение. А у вас?
        go: /helloBye/whatsUp



    state: bye
        # 4EX: пока, металлолом
        q!: [$beginningWords][*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll] [ладно/лан/ясн*/понятн*][давай] (пока/пака/пок/покэ/пакэ/покка/покаа/пакка/пакаа/чмоки/чмоке/бывай/exit/до связи/конец связи) [*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll]
        q!: [$beginningWords][*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll] [ладно/лан/ясн*/понятн*][давай] до (вечера/скорого/[скор*] встреч*/след* (недели/раза)) [*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll]
        q!: * $goodBye *
        # 4EX: я тебя закрываю
        q!: * я тебя закрываю *
        a: Всего доброго!



    state: wishYouBest
        # 4EX: всего хорошего, робот
        q!: [$beginningWords][ну все][*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll] [ладно/лан/ясн*/понятн*][давай]   (всего хорошего/(удачного/хорошего) [тебе/вам] дня/успехов [тебе/вам]) [*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll]
        q!: [$beginningWords][ну все][*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll] [ладно/лан/ясн*/понятн*][давай]   (будь (здоров/здорова/здаров/здароф/здороф)/будьте здоровы) [*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll]
        # 4EX: будь здоров
        q!: [$beginningWords] (будь (здоров/здорова/здаров/здароф/здороф)/будьте здоровы)
        q!: [$beginningWords]  (всего хорошего/(удачного/хорошего) [тебе/вам] дня/успехов [тебе/вам])
        a: Спасибо! И вам!



    state: gottago
        # 4EX: я отчаливаю
        q!: * $gottago *
        # 4EX: лан пока
        q!: [$beginningWords][ну все][*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll] [ладно/лан/ясн*/понятн*][давай] ([я](иду спать/мне пора)/(щасливо/щаслива/сщастливо/счастливо) [оставт*])[*ой/*ый/*ка][робот/бот/железяка/железка/металлолом/металолом/недо*] [$botVocAll]
        q!: ([я](иду спать/мне пора)/(щасливо/щаслива/сщастливо/счастливо) [оставт*])
        a: Всего доброго!



    state: goodNight
        # 4EX: споки
        q!: * $goodNight2 *
        q!: * (спокночи/споки/спок/гуднайт/гуд найт) *
        # 4EX: я иду спатки
        q!: * {(я/мы/йа) [пошел/пашел/пошла/пашла/ушел/ушол/ушла/пойду/пайду/иду] (спать/спатки/спатеньки/спатеки)} *
        a: Спокойной ночи!

